plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'application'
}
application {
    mainClassName("nju.pt.server.MainKt")
}

// 启动脚本中添加pause指令
tasks.startScripts {
    doLast {
        def windowsScriptFile = getWindowsScript()
        windowsScriptFile.text = windowsScriptFile.text.replace("\n:end", "pause\n\n:end")
//        print(windowsScriptFile.text)
    }
}
// 打包时加入README.md
distributions {
    main {
        contents {
            from parent.file('README_Server.md')
            parent.file("assets").listFiles().each {
                from(it) {
                    into "assets"
                }
            }

        }
    }
}

group = 'nju.pt'
version = '1.3.0'

repositories {
    mavenCentral()
}
dependencies {
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
//    日志
    implementation 'org.slf4j:slf4j-api:1.7.36'
    implementation 'ch.qos.logback:logback-core:1.2.11'
    implementation 'ch.qos.logback:logback-classic:1.2.11'
//    kotlinx的json序列化库
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.3"
//    poi的日志组件依赖
    implementation 'org.apache.logging.log4j:log4j-core:2.17.2'
//    poi 用于读写Excel文件
    implementation "org.apache.poi:poi:5.2.2"
    implementation 'org.apache.poi:poi-ooxml:5.2.2'

    implementation project(":DatabaseAssist")
    // 网络文件传输
    implementation project(":FileNetAssist")

}

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions.jvmTarget = '17'
}

compileTestKotlin {
    kotlinOptions.jvmTarget = '17'
}